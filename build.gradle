plugins {
    id 'idea'
    id 'java'
    id 'org.springframework.boot' version '3.4.13'
    id 'io.spring.dependency-management' version '1.1.7'
    // Lombok
    id 'io.freefair.lombok' version '9.1.0'
    // JaCoCo
    id 'jacoco'
    // SonarQube
    id 'org.sonarqube' version '7.2.2.6593'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

// dependency versions
def ulidCreatorVersion = '5.2.3'
def jacksonDatabindVersion = '3.0.2'
def springDocOpenApiVersion = '2.8.13'
def querydslVersion = '5.1.0'
def p6spyVersion = '1.12.0'
def jjwtVersion = '0.13.0'
def autoParamsVersion = '11.3.1'
def fixtureMonkeyVersion = '1.1.15'
def datafakerVersion = '2.5.1'
def jqwikVersion = '1.9.3'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'

    // ULID Creator
    implementation "com.github.f4b6a3:ulid-creator:$ulidCreatorVersion"

    // Jackson Datatype: JSR310
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // Jackson Databind
    implementation "tools.jackson.core:jackson-databind:$jacksonDatabindVersion"

    // SpringDoc OpenAPI Starter WebMVC UI
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springDocOpenApiVersion"

    // Querydsl JPA Support
    implementation "com.querydsl:querydsl-jpa:$querydslVersion:jakarta"
    // Querydsl APT Support
    annotationProcessor "com.querydsl:querydsl-apt:$querydslVersion:jakarta"
    // Jakarta Annotations API
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    // Jakarta Persistence API
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // P6Spy Spring Boot Starter
    implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:$p6spyVersion"

    // JJWT :: API
    implementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
    // JJWT :: Impl
    runtimeOnly "io.jsonwebtoken:jjwt-impl:$jjwtVersion"
    // JJWT :: Extensions :: Jackson
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"

    // Apache HttpClient
    implementation 'org.apache.httpcomponents.client5:httpclient5'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly "com.h2database:h2"
    runtimeOnly "com.mysql:mysql-connector-j"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AutoParams
    testImplementation "io.github.autoparams:autoparams:$autoParamsVersion"
    // AutoParams Lombok
    testImplementation "io.github.autoparams:autoparams-lombok:$autoParamsVersion"
    // AutoParams Mockito
    testImplementation "io.github.autoparams:autoparams-mockito:$autoParamsVersion"

    // Fixture Monkey
    testImplementation "com.navercorp.fixturemonkey:fixture-monkey-autoparams:$fixtureMonkeyVersion"
    testImplementation "com.navercorp.fixturemonkey:fixture-monkey-jackson:$fixtureMonkeyVersion"
    testImplementation "com.navercorp.fixturemonkey:fixture-monkey-javax-validation:$fixtureMonkeyVersion"
    testImplementation "com.navercorp.fixturemonkey:fixture-monkey-mockito:$fixtureMonkeyVersion"
    testImplementation "com.navercorp.fixturemonkey:fixture-monkey-starter:$fixtureMonkeyVersion"

    // Datafaker
    testImplementation "net.datafaker:datafaker:$datafakerVersion"

    // Jqwik
    testImplementation "net.jqwik:jqwik:$jqwikVersion"
    // Jqwik API
    testImplementation "net.jqwik:jqwik-api:$jqwikVersion"
    // Jqwik Engine
    testImplementation "net.jqwik:jqwik-engine:$jqwikVersion"
    // Jqwik Time
    testImplementation "net.jqwik:jqwik-time:$jqwikVersion"
    // Jqwik Testing
    testImplementation "net.jqwik:jqwik-testing:$jqwikVersion"
}

test {
    useJUnitPlatform {
        def includeTagsProp = project.findProperty("includeTags")
        def excludeTagsProp = project.findProperty("excludeTags")

        if (includeTagsProp) includeTags includeTags.toString().split(',').collect { it.trim() } as String[]
        if (excludeTagsProp) excludeTags includeTags.toString().split(',').collect { it.trim() } as String[]
    }
    systemProperty 'user.timezone', 'Asia/Seoul'
}

jar {
    enabled = false
}

bootJar {
    archiveFileName = 'app.jar'
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    def Qdomains = []
    for (qPattern in "**/QA".."**/QZ") Qdomains.add(qPattern + "*")

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    "**/*Application*",
                    "**/config/**",
                    "**/*Dto*",
                    "**/*Request*",
                    "**/*Response*",
                    "**/dto/**",
                    "**/*Interceptor*",
                    "**/*Exception*",
                    "**/*Properties*",
                    "**/model/**"
            ] + Qdomains)
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            excludes = [
                    "**.*Application*",
                    "**/config/**",
                    "**.*Dto*",
                    "**.*Request*",
                    "**.*Response*",
                    "**/dto/**",
                    "**.*Exception*",
                    "**.*Properties*",
                    "**/Q*",
                    "**/model/**"
            ]

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }
        }
    }
}

sonar {
    properties {
        property 'sonar.projectKey', ''
        property 'sonar.host.url', ''
        property 'sonar.token', ''
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        def coverageExclusions = [
                '**/*Application.java',
                '**/config/**',
                '**/*Dto.java',
                '**/*Request.java',
                '**/*Response.java',
                '**/dto/**',
                '**/*Interceptor.java',
                '**/*Exception.java',
                '**/*Properties.java',
                '**/Q*.java',
                '**/model/**'
        ]
        property 'sonar.coverage.exclusions', coverageExclusions.join(',')
    }
}
